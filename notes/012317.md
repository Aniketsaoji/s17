# CSCI 3366 Programming Languages

### Spring 2017

**R. Muller**

------

### Lecture Notes: 3

#### Today

Getting Started with OCaml Part 1: The Basics

1. Basic Types, Literals & Expressions; Simplification & Values

2. Naming Values

3. Libraries

4. Defining Functions

   ​

---

A programming language is a system that allows a programmer to express ideas related to computation and information. At the present time, most programming languages, including OCaml, are text-based: the program code in the language is written out in text (much like these notes) and is then processed by the computer to carry out the expressed computation. For simple OCaml programs this process can carried out by an **interpreter** which:

 1. READs: prompts the user for and reads an OCaml expression
 2. EVALUATEs: evaluates the expression to find its value if it has one
 3. PRINTs: print the value to the shell
 4. LOOPs back to step 1.

For example, an interaction with OCaml's evaluate shell might look like:

```ocaml
# 3 * 2;;
- : int = 6

# 
```

The hash-sign "#" is OCaml's prompt and the trailing ";;" is the user's signal to OCaml that they'd like the expression to be evaluated. Since the 3-step (read-eval-print) process is done over and over in a *loop*, the setup is usually called a **REPL**.



----------------

### 1. Basic Types, Literals & Expressions, Simplification & Values###

In computing, **types** can be understood as sets of "things" that are the subject of the computation. All programming languages provide a number of basic types that are built-in to the language. Most programming languages have ways to define new types. We'll get to that topic later.

Virtually all programming languages have basic types **int** and **float** together with built-in operators (e.g., +, -, *, /, mod) that work on values of that type.

**The int Type**

```ocaml
# 2;;
- : int = 2
```

The numeral `2` is a **literal** expression of type int.

```ocaml
# 5 * 2;;
- : int = 10
```

The expression `5 * 2` simplifies to the literal `10`. Since `10` is a literal, it can't be further simplified. Expressions that can't be further simplified are called **values**.

*CONVENTION*: Infix operators should usually have spaces on either side.  So `5 / 2` is good, `5/2` is less good, and both `5 /2` and `5/ 2` are bad. Some coders prefer a style in which operators of lower precedence are
surrounded by spaces while operators of higher precedence do not.  For example, instead of

```ocaml
                                       a * x + b * x + c
```

one might write:

```ocaml
                                         a*x + b*x + c
```

but the former is preferred.


**Integer Division**

```ocaml
# 7 / 2;;
- : int = 3

# 99 / 100;;
- : int = 0

# 1 / 0;;
Exception: Division_by_zero.

# 7 mod 5;;
- : int = 2

# (+);;
- : int -> int -> int = <fun>

# (+) 2 3;;
- : int = 5
```

*Comments*: 

1.  In the last two examples: the infix operators `+, *, /, -, …` can be used in *prefix* *position* by enclosing them in parentheses as shown. 
2. The type of the `+` operator is `int -> int -> int` — this may look a little strange; we'll explain it later.
3. NB: the multiply operator must be surrounded by spaces `( * )` so it isn't confused with

```ocaml
# (* A comment intended for the human reader *)
# ( * ) 2 3;;
- : int = 6
```

​	This is also true for the exponentiation operator `**`.

**The float Type**

```ocaml
# 3.14;;
- : float = 3.14

# 314e-2;;
- : float = 3.14

# 2.0 *. 3.14;;               (* NB: The floating point operators: +., -, ... end with a . *)
- : float = 6.28

# 7.0 /. 2.0;;
- : float = 3.15

# 2.0 ** 3.0;;
- : float = 8.0

# infinity;;
- : float = infinity

# 1.0 /. 0.0;;
- : float = infinity

# 1.0 /. 2;;
Error: This expression has type int but an expression was expected of type float
```



**Type Conversion**

```ocaml
# float_of_int 1;;
- : float = 1.0

# float 1;;
- : float = 1.0

# int_of_float 1.9999;;
- : int = 1
```

The built-in operators are interpreted using the familiar PEMDAS order for the operators.

```ocaml
# 2.0 +. 3.14 *. 4.0;;
- : float = 14.56

# (2.0 +. 3.14) *. 4.0;;
- : float = 20.5600000000000023          (* NB: 15 decimal places for single precision floats *)
```



**The unit Type**

```ocaml
# ();;
- : unit = ()
```



**The char Type**

```ocaml
# 'M';;
- : char = 'M'

# Char.code 'M';;
- : int = 77

# Char.chr 77;;
- : char = 'M'
```

The last two examples uses functions from the built-in `Char` library, part of the standard library that ships with every OCaml installation. We'll have more to say about libraries below.

The string type and the bool type are built-in "base" types but they're actually a bit different than the base types shown above. For now we'll ignore the difference.

**The string Type**

```ocaml
# "hello world!";;
- : string = "hello world!"

# "hello" ^ " world!";;
- : string = "hello world!"

# string_of_int 123;;
- : string = "123"

# String.length "hello world!";;
- : int = 12
```



**The bool Type**

```ocaml
# true;;
- : bool = true

# false;;
- : bool = false

# false || true;;
- : bool = true

# false && true;;
- : bool = false

# not (false && true);;
- : bool = true
```



**Simplification**

OCaml's modus operandi is to simply expressions in search of their values (if they have one).

```ocaml
2 + 3 + 4 + 5 -->
5 + 4 + 5 -->
9 + 5 -->
14
```

This particular simplification process required 3 computation steps.

**Summary**

- OCaml expressions are mostly familiar, being built-up from literals of base type, operators and parentheses;
- OCaml's modus operandi is to simplify; 
- A **value** is an expression that cannot be further simplified.

For now, the containment is:

```
  +--------------------------+
  |    Expressions           |
  | +----------------------+ |
  | |   Values             | |
  | | +------------------+ | |
  | | | Literals         | | |
  | | +------------------+ | |
  | +----------------------+ |
  +--------------------------+
```

Every literal is a value and every value is an expression.  There are expressions that are not values and (as we'll see) there are values that are not literals. Later on we will alter this diagram a bit.



-------------------

### 2. Naming Values###

In OCaml, values can be named using a **let**-expression. There are two basic forms:

```ocaml
  let x = expr                                    --- top-level let
  let x = expr1 in expr2                          --- let-in                 
```

The former makes the name `x` usable throughout the top-level. In the latter, the name `x` is only usable in the expression immediately after the `in`  keyword: i.e., the expression `expr2`.

```ocaml
# let a = 3.0 *. 2.0;;                  (* top level let *)
val a : float = 6.0

# a;;
- : float = 6.0

# let b = 3.0 *. 2.0 in b ** 2.0;;      (* let-in *)
- : float = 36.0

# b;;
Error: Unbound variable b

let pi = acos (-1.0);;
val pi : float = 3.14159265358979312
```

**Name Formation** Names in OCaml can be formed in the standard ways. Names beginning with a lowercase letter are used for values and types; names beginning with an uppercase letter are used for module names, signature names or constructors. We'll discuss these in the next section.

**Conventions** 

1. **Name Formation**: OCaml coders tend to follow the standard conventions using either **camelCase** or underscores **another_readable_name** for variable names.
2. **Cascaded Lets**: It's very common to have a cascade of let-expressions as in

```ocaml
  let x1 = expr1 in let x2 = expr2 in ... let xn = exprn in expr
```

​	In this case we stack up the lets as follows:

```ocaml
  let x1 = expr1 in
  let x2 = expr2 in
  ...
  let xn = exprn
  in
  expr
```



---

### 3. Module & Package Libraries###

Like most programming languages, OCaml has libraries of (free) code that can be used more or less off-the-shelf. The [Standard Library](http://caml.inria.fr/pub/docs/manual-ocaml/stdlib.html) comes with all implementations of OCaml. We've referred to the Standard Library's **Char** and **String** modules already, there are many more: a **Random** module for working with random numbers, a  **Printf** module for formatted printing and so on. It's worth noting that OCaml's Standard Library is pretty spare in comparison to standard library's in other programming languages.

Names defined in library modules can be accessed by prefixing their names with the module name. 

```ocaml
# Random.int 3;;                 (* equal chance of 0, 1 or 2 *)
- : int = 1

# Random.float 1.0;;
- : float = 0.432381 

# Printf.sprintf "My friend %s is %d years old." "Jasmine" 19;;
- : string : "My friend Jasmine is 19 years old."
```

One can omit the module-name prefix by opening the module.

```ocaml
open Printf
# sprintf "Hello %s" " world!";;
- : string = "Hello world!"
```

**Pervasives**

Some library resources are used so, well, pervasively, in OCaml code, their definitions are housed in a module [Pervasives](http://caml.inria.fr/pub/docs/manual-ocaml/libref/Pervasives.html) that is opened automatically. Instead of requiring `Pervasives.min`, one can write simply `min`.

```ocaml
# let smaller = min 2 3;;
val smaller : int = 2

# floor 3.9999;;
- : int = 3

# abs (-3);;
- : int = 3

# abs_float (-2.0);;
- : float = 2.
```

Modules that are not part of the Standard Library need to be made known to the OCaml compiler. This can be done in a number of ways, we'll come back to this topic later.

**Packages**

The world-wide OCaml community has developed many more library modules that provide functionality beyond the Standard Library. These libraries usually take the form of **packages** that implement complete applications or services. OCaml packages can be installed and managed by OCaml's package manager [OPAM](https://opam.ocaml.org/). 



-------------------

### 4. Defining Functions###

Functions are the main tool for packaging up computation in almost all programming languages. In this section we'll introduce the basic form. In the next section we'll discuss how to write functions in general. Functions have **definitions** and **uses**.  

**Function Definitions** The two variations of the let-expression discussed earlier can be used to name a function value.

```ocaml
  let functionName x1 ... xn = expr1                    --- top-level 
  let functionName x1 ... xn = expr1 in expr2           --- let-in
```

The symbols `x1`, ..., `xn` denote binding occurrences of **variables**. These occurrences are called **parameters** or **formal  parameters**. Formal parameters usually govern applied occurrences or uses of the variables in the function **body**  `expr1`. 

**Notes**: 

1. The variables `x1`, …, `xn` can be used only in `expr1`;
2. The function name `functionName` cannot be used in `expr1`, in the former case it can be used in the top-level and in the latter case it can be used only in `expr2`.

Function definitions are usually stored in text files with the **.ml** extension.

**Function Calls** A function use, **call** or **application** has the form:

```ocaml
  (functionName expr1 ... exprn)
```

where each of `expr1`, ..., `exprn` is an expression. How does a function do its job? As with the simple algebraic expressions, through simplification. 

1. When a function call is evaluated, for each i = 1,..,n, `expri` is evaluated. This process may produce a value Vi, it may encounter an error or it may not stop (more on that later).

2. If for each i, the evaluation of `expri` produces a value Vi, the value Vi is plugged-in for `xi` in the function body (i.e., each occurrences of `xi` in `expr1` is **replaced** by value Vi).

3. Then the body of the function is simplified. If the body of the function has a value V, then V is the value of the function call.


**Example**: a doubling function

```ocaml
# let double n = n * 2;;
val double : int -> int

double (2 + 2) -->              --- simplification requires 3 steps
double 4 -->
4 * 2 -->
8
```

```ocaml
double 3 + double (2 + 3) -->   --- simplification requires 6 steps
3 * 2 + double (2 + 3) -->
6 + double (2 + 3) -->
6 + double 5 -->
6 + 5 * 2 -->
6 + 10 -->
16
```

**Example**: A min3 function that uses the two-argument pervasive `min` function to compute the minimum of 3 inputs.

```ocaml
# let min3 p q r = min p (min q r);;
val min3 : 'a -> 'a -> 'a -> 'a

min3 1 2 3 -->
min 1 (min 2 3) -->
min 1 2 -->
1
```

**Recursive Functions**

In OCaml, the natural way to express repetition in a function is to define it recursively. In order to define a recursive function, the keyword `rec` is required as an annotation:

```ocaml
  let rec functionName x1 ... xn = expr1                    --- top-level 
  let rec functionName x1 ... xn = expr1 in expr2           --- let-in
```

In a recursive function, the function name can be used in `expr1`. We'll have more to say about recursive functions in the next section.






















